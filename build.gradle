buildscript {
   repositories {
      mavenCentral()
      jcenter()
   }
   dependencies {
      classpath "org.eclipse.persistence:javax.persistence:2.1.0"
      classpath "org.eclipse.persistence:org.eclipse.persistence.jpa:2.5.2"
      classpath "org.springframework.security:spring-security-core:3.2.5.RELEASE"
      classpath "org.neo4j:neo4j:2.2.0-M04"
      classpath "org.jboss.arquillian.gradle:arquillian-gradle-plugin:0.1"
      classpath "org.apache.commons:commons-dbcp2:2.0.1"
      classpath "org.hsqldb:hsqldb:2.3.2"
   }
}

subprojects {
   apply plugin: 'java'

   tasks.withType(JavaCompile) {
      options.incremental = true
      sourceCompatibility = '1.7'
      targetCompatibility = '1.7'
   }

   repositories {
      mavenCentral()
      jcenter()
      flatDir { dirs '../lib' }
   }

   dependencies {
      compile("org.slf4j:slf4j-api:")
      compile("org.eclipse.persistence:javax.persistence:")
      compile("org.eclipse.persistence:org.eclipse.persistence.jpa:")
      compile("javax.validation:validation-api:1.0.0.GA")
      compile("org.springframework.security:spring-security-core:")

      runtime("ch.qos.logback:logback-classic:1.1.2")
   }

   configurations.all {
      resolutionStrategy.eachDependency { DependencyResolveDetails details ->
         if (details.requested.group == 'org.hsqldb') {
            details.useVersion '2.3.2'
         } else if (details.requested.group == 'org.slf4j') {
            details.useVersion '1.7.10'
         } else if (details.requested.group == 'commons-logging') {
            details.useVersion '1.1.3'
         } else if (details.requested.group == 'javax.inject') {
            details.useVersion '1'
         } else if (details.requested.group == 'org.springframework') {
            details.useVersion '4.1.4.RELEASE'
         } else if (details.requested.group == 'org.springframework.security') {
            details.useVersion '3.2.5.RELEASE'
         } else if (details.requested.group == 'org.thymeleaf') {
            details.useVersion '2.1.4.RELEASE'
         } else if (details.requested.group == 'org.apache.bval') {
            details.useVersion '0.5'
         } else if (details.requested.group == 'org.aspectj') {
            details.useVersion '1.8.4'
         } else if (details.requested.group == 'junit') {
            details.useVersion '4.12'
         } else if (details.requested.group == 'javax.servlet') {
            details.useVersion '3.0.1'
         } else if (details.requested.name == 'javax.persistence') {
            details.useVersion '2.1.0'
         } else if (details.requested.group == 'org.eclipse.persistence') {
            details.useVersion '2.5.2'
         } else if (details.requested.group == 'org.neo4j') {
            details.useVersion '2.2.0-M04'
         } else if (details.requested.group == 'org.mockito') {
            details.useVersion '1.10.19'
         }
      }
   }


   // marker task for production specific builds
   task release

   // arquillianRun alias
   task run
}

project(':datr-webapp') {
   dependencies {
      compile project(path: ':datr-model', configuration: 'woven')
   }
}




// Gradle wrapper task
// Only used when in need to reinstall the wrapper
task wrapper(type: Wrapper) {
   gradleVersion = '2.2.1'
}
